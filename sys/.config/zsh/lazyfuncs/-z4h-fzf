#!/usr/bin/env zsh

local -a cmd flags bindings
cmd=(/usr/bin/fzf "$@")

typeset -gr _z4h_opt='emulate -L zsh &&
  setopt typeset_silent pipe_fail extended_glob prompt_percent no_prompt_subst &&
  setopt no_prompt_bang no_bg_nice no_aliases'

local -A keys=(
  ctrl-h     backward-kill-word
  alt-j      clear-query
  ctrl-u     clear-query
  ctrl-k     kill-line
  alt-k      unix-line-discard
  ctrl-space toggle-out
  ctrl-a     toggle-all
)

keys+=(
  tab      up
  btab     down
  ctrl-r   up
  ctrl-s   down
)

local kv
for kv in $bindings; do
  kv=(${(s.:.)kv})
  (( $#kv == 2 )) || continue
  keys[$kv[1]]=$kv[2]
done

local k v
local -i expect
for k v in ${(kv)keys}; do
  if [[ +$v+ == *+repeat+* ]]; then
    flags=(--expect=$k "${flags[@]}")
    expect=1
    if [[ $v == repeat(|+*) ]]; then
      keys[$k]=ignore
    else
      keys[$k]=${v%%+repeat(|+*)}
    fi
  fi
done

local keymap
printf -v keymap '%s:%s,' ${(kv)keys}
local out=$("${cmd[@]}" "$@" --bind=${keymap%,} "${flags[@]}")
[[ -n $out ]] || return
(( expect )) || print
print -r -- $out
