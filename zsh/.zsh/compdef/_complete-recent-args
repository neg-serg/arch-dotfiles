#compdef -k menu-select ^x.
zmodload zsh/parameter

function _complete-recent-args() {
    if zstyle -t ':complete-recent-args' use-histbang; then
        _complete-recent-args-histbang
    else
        _complete-recent-args-regular
    fi
}

function _complete-recent-args-regular() {
    # add current edit buffer's words if exists
    local w
    for w in "${(@)words}"; do
        if [[ -n "${w}" ]]; then
            compadd -Q -2 -V current -- "${w}"
        fi
    done

    # add last 10 command lines' words
    integer i
    for i in "${(@)${(Onk@)history}[1,10]}"; do
        compadd -Q -2 -V $i -- "${(z@)history[$i]}"
    done

}

function _complete-recent-args-histbang() {
    # add current edit buffer's words if exists
    local w
    w=( ${words:#} )
    if [[ ${#w} > 0 ]]; then
        compadd -Q -2 -V current -d w -r ':*\-' -- '!#:'{0..$[ ${#w} -1]}
    fi

    # add last 10 command lines' words
    integer i
    local d
    for i in "${(@)${(Onk@)history}[1,10]}"; do
        d=( ${(z@)history[$i]} )
        compadd -Q -2 -V $i -d d -r ':*\-' -- '!'$^i:{0..$[ ${#d} -1]}
    done
}

_complete-recent-args "$@"
