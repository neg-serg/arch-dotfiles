#!/usr/bin/env zsh
# Author: Dolores Portalatin <hello@doloresportalatin.info>
# Dependencies: imagemagick, i3lock-color-git, scrot, wmctrl (optional)
set -o errexit -o noclobber -o nounset

hue=(-level "0%,100%,0.6")
effect=(-filter Gaussian -resize 20% -define "filter:sigma=1.5" -resize 500.5%)
# effect=(-scale 10% -blur 0x2.5 -resize 1000%)
# effect=(-scale 2.5% -resize 4000%)
font="Iosevka-Medium-Italic"
image=$(mktemp --suffix=.jpg)
shot=(maim -m 3)
i3lock_cmd=(i3lock -i "$image")

opts="Options:
    -h, --help       This help menu.
    -g, --greyscale  Set background to greyscale instead of color.
    -f <fontname>, --font <fontname>  Set a custom font.
    -t <text>, --text <text> Set a custom text prompt.
    -l, --listfonts  Display a list of possible fonts for use with -f/--font.
                     Note: this option will not lock the screen, it displays
                     the list and exits immediately.
    -n, --nofork     Do not fork i3lock after starting.
    --               Must be last option. Set command to use for taking a
                     screenshot. Default is 'import -window root'. Using 'scrot'
                     or 'maim' will increase script speed and allow setting
                     custom flags like having a delay."

# move pipefail down as for some reason "convert -list font" returns 1
set -o pipefail
trap 'rm -f "$image"' EXIT
temp="$(getopt -o :hdnpglt:f: -l help,listfonts,nofork,pixelate,greyscale,text:,font: --name "$0" -- "$@")"
eval set -- "$temp"

text="Type password to unlock"
case "${LANG:-}" in
    af_*) text="Tik wagwoord om te ontsluit" ;; # Afrikaans
    cs_*) text="Pro odemčení zadajte heslo" ;; # Czech
    de_*) text="Bitte Passwort eingeben" ;; # Deutsch
    en_*) text="Type password to unlock" ;; # English
    es_*) text="Ingrese su contraseña" ;; # Española
    fr_*) text="Entrez votre mot de passe" ;; # Français
    hi_*) text="अनलॉक करने के लिए पासवर्ड टाईप करें" ;; #Hindi
    id_*) text="Masukkan kata sandi Anda" ;; # Bahasa Indonesia
    it_*) text="Inserisci la password" ;; # Italian
    ja_*) text="パスワードを入力してください" ;; # Japanese
    lv_*) text="Ievadi paroli" ;; # Latvian
    nb_*) text="Skriv inn passord" ;; # Norwegian
    pl_*) text="Podaj hasło" ;; # Polish
    ru_*) text="Введите пароль" ;; # Russian
    zh_*) text="请输入密码以解锁" ;; # Chinese
    *) text="Type password to unlock" ;; # Default to English
esac

while true ; do
    case "$1" in
        -h|--help) printf "Usage: %s [options]\n\n%s\n\n" "${0##*/}" "$opts"; exit 1 ;;
        -g|--greyscale) hue=(-level "0%,100%,0.6" -set colorspace Gray -average) ; shift ;;
        -f|--font)
            case "$2" in
                "") shift 2 ;;
                *) font=$2 ; shift 2 ;;
            esac ;;
        -t|--text) text=$2 ; shift 2 ;;
        -l|--listfonts) convert -list font | awk -F: '/Font: / { print $2 }' | sort -du | command -- ${PAGER:-less}; exit 0 ;;
	-n|--nofork) i3lock_cmd+=(--nofork) ; shift ;;
        --) shift; shot_custom=true; break ;;
        *) echo "error" ; exit 1 ;;
    esac
done
${shot[@]} "$image"
value="60" #brightness value to compare to
color=$(MAGICK_OCL_DEVICE=false OMP_NUM_THREADS=32 \
    convert "$image" -gravity center -crop 100x100+0+0 +repage -colorspace hsb \
    -resize 1x1 txt:- | awk -F '[%$]' 'NR==2{gsub(",",""); printf "%.0f\n", $(NF-1)}');

if [[ $color -gt $value ]]; then #white background image and black text
    bw="black"
    icon="/usr/share/i3lock-fancy/icons/lockdark.png"
    param=("--inside-color=0000001c" "--ring-color=0000003e" \
        "--line-color=00000000" "--keyhl-color=ffffff80" "--ringver-color=ffffff00" \
        "--separator-color=22222260" "--insidever-color=ffffff1c" \
        "--ringwrong-color=ffffff55" "--insidewrong-color=ffffff1c" \
        "--verif-color=ffffff00" "--wrong-color=ff000000" "--time-color=ffffff00" \
        "--date-color=ffffff00" "--layout-color=ffffff00")
else #black
    bw="white"
    icon="/usr/share/i3lock-fancy/icons/lock.png"
    param=("--inside-color=ffffff1c" "--ring-color=ffffff3e" \
        "--line-color=ffffff00" "--keyhl-color=00000080" "--ringver-color=00000000" \
        "--separator-color=22222260" "--insidever-color=0000001c" \
        "--ringwrong-color=00000055" "--insidewrong-color=0000001c" \
        "--verif-color=00000000" "--wrong-color=ff000000" "--time-color=00000000" \
        "--date-color=00000000" "--layout-color=00000000")
fi
MAGICK_OCL_DEVICE=false OMP_NUM_THREADS=32 convert \
    "$image" "${hue[@]}" "${effect[@]}" \
    -font "$font" -pointsize 26 -fill "$bw" -gravity center \
    -annotate +0+160 "$text" "$icon" -gravity center -composite "$image"
# try to use i3lock with prepared parameters
if ! "${i3lock_cmd[@]}" "${param[@]}" >/dev/null 2>&1; then
    # We have failed, lets get back to stock one
    "${i3lock_cmd[@]}"
fi
